apply plugin: 'com.android.application'

// Comment this to disable Firebase Analytics or if you do not have the necessary
// google-services.json file for Firebase.
apply plugin: 'com.google.gms.google-services'

// The directory where Engage-specific items are stored
def engageEngineDir = "libs/"

// The version of the Engage Engine we want to use
def engageEngineVersion = "1.140.8903"

// The build type of the Engage Engine we want
def engageEngineBuildType = "release"

// Addition flavors that we want to build for
def additionalFlavorsFile = "flavors/flavors.gradle"

// The application ID in the final APK.  Change this string for a different
// APK package name .
def apkPackageName = "com.rallytac.engageandroid.reference"

android {
    flavorDimensions "version"

    productFlavors {
        // This is the Rally Tactical Systems "reference" flavor
        reference {
            dimension "version"
        }
    }

    // Add additional OEM flavors if any
    try {
        apply from: additionalFlavorsFile
    }
    catch(ignored) {
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId apkPackageName
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1204
        versionName "1204"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def apkBaseName = variant.productFlavors.get(0).name
            def SEP = "_"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionCode
            def date = new Date();
            def formattedDate = date.format('yyyyMMdd_HHmmss')
            def newApkName = apkBaseName + SEP + version + SEP + buildType + SEP + formattedDate + ".apk"

            outputFileName = new File(newApkName)
        }
    }
}

dependencies {
    // Engage Engine
    implementation fileTree(dir: engageEngineDir, include: ["engage-engine-" + engageEngineBuildType + "-" + engageEngineVersion + ".aar"])

    // Android goodies
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // QR codes
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    // Google Maps
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'


    // Charting
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-core:17.2.3'
}
